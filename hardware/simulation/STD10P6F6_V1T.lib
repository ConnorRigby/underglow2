*******************************************************************
******STMicroelectronics MOSFET, IGBT and Bipolar Library *********
*******************************************************************
*											*
* Models provided by STMicroelectronics are not guaranteed to	*
* fully represent all the specifications and operating		*
* characteristics of the product behavior that they reproduce.	*
* The model describes the characteristics of a typical device.	*
* In all cases, the current product data sheet contains all		*
* information to be used like final design guidelines and the	*
* only actual performance specification.					*
* Altough models can be a useful tool in evaluating device		*
* performance, they cannot model exact device performance under	*
* all conditions.									*
* STMicroelectronics therefore does not assume any			*
* responsibility arising from their use.					*
* STMicroelectronics reserves the right to change models		*
* without prior notice.								*
*											*
*******************************************************************

* MODELLING FOR STD10P6F6_V1T
 
.SUBCKT STD10P6F6_V1T 1 2 3 Tj Tcase

EVTH 4 4x VALUE ={(((2.25m*v(tj))-0.055))}

G_rdrain 7 6  VALUE ={v(7,6)/(51E-03*(1+0.0125*v(TJ)))}

LG 2 4  4.5n
LS 12 3 4.5n
LD 6x 1  2.1n
V_sense 6x 6 0
RG 4x 5  3
RS 9 12 1E-04
RD 7 6  100
RJ 8 7  1E-04
CGS 5 9   3E-10
CGD 7 10  2.5E-10
CK  11 7  3.5E-11
DGD 7 11 DGD
DBS 6 12 DBS
Dblock 12 122 DI
Ebvdss 123 6 VALUE ={65*(1+2m*v(Tj))}
Rav 122 123 100m
DBD  7 9 DBD
MOS  13 5 9 9 MOS L=1u W=1u
E1  10 5 101 0 1
E2  11 5 102 0 1
E3  13 8 POLY(2) 6 8 6 12 0 0 0 0  0.144
G1  100 0 5 7 1u
D1  101 100  DID
D2  100 102  DID
R1  101 0  1MEG
R2  102 0  1MEG
 
.MODEL MOS PMOS
+ LEVEL = 3
+ VTO   = -3.1
+ PHI   = 0.919
+ IS    = 0.1P
+ JS    = 0
+ THETA = 0.418
+ KP    = 9
+ eta   = 0.000144E-02

.MODEL DGD D
+ IS    = 0.1P
+ CJO   = 0.594E-13
+ VJ    = 0.686
+ M     = 0.364
.MODEL DBD D
+ IS    = 0.1P
+ CJO   = 0.738E-13
+ VJ    = 0.804
+ M     = 0.338
.MODEL DBS D
+ IS    = 0.1P
+ BV    = 168
+ N     = 1
+ TT    = 19E-09
+ RS    = 0.193
.MODEL DID D
+ IS    = 0.01P
+ RS    = 0
+ BV    = 168

.MODEL DI D
+ IS    = 1P
+ RS    = 0
+ BV    = 350

***************  Thermal Net Junction Case  ***************

E_Pw thx 0 VALUE ={(I(V_sense)*v(1,3))}

RTHXX thx thx1 1k
CTXX thx1 0 1n IC=0

G_pw2 0 TJ VALUE ={Limit((I(V_sense)*v(1,3)),0,100E3)}

Rbazz2 Tj 0 1e6

Rth1 Tj  Th1    0.81E-02
Rth2 Th1 Th2    0.71E-01
Rth3 Th2 Th3    0.47
Rth4 Th3 Th4    1.81
Rth5 Th4 Tcase  1.92

Cth1 Tj  0   1.03E-06
Cth2 Th1 0   1.05E-05
Cth3 Th2 0   2.95E-04
Cth4 Th3 0   8.18E-04
Cth5 Th4 0   4.25E-03
Cth6 Tcase 0 1E-02

.ENDS STD10P6F6_V1T
 
* END OF MODELLING
